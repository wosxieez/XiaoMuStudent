package com.xiaomu.view.otherview
{
	import com.xiaomu.data.Answer;
	import com.xiaomu.data.Question;
	import com.xiaomu.manager.DataManager;
	import com.xiaomu.manager.ServiceManager;
	import com.xiaomu.renderer.AnswerItemRenderer;
	
	import flash.events.MouseEvent;
	
	import coco.component.Button;
	import coco.component.List;
	import coco.component.Panel;
	import coco.component.TextArea;
	import coco.data.Message;
	import coco.manager.PopUpManager;
	import coco.util.CocoUI;
	
	/**
	 * 问题视图 
	 * @author coco
	 * 
	 */	
	public class QuestionView extends Panel
	{
		public function QuestionView()
		{
			super();
			
			width = 500;
			height = 350;
			title = "问题作答";
		}
		
		//---------------------------------------------------------------------------------------------------------------------
		//
		//  Get Instance
		//
		//---------------------------------------------------------------------------------------------------------------------
		
		private static var instance:QuestionView;
		
		public static function getInstance():QuestionView
		{
			if (!instance)
				instance = new QuestionView();
			
			return instance;
		}
		
		
		//---------------------------------------------------------------------------------------------------------------------
		//
		//  Variables
		//
		//---------------------------------------------------------------------------------------------------------------------
		
		private var questionInput:TextArea;
		private var answerList:List;
		private var cancelButton:Button;
		private var okButton:Button;
		private var currentQuestion:Question;
		private var isUpdate:Boolean = false;
		
		//---------------------------------------------------------------------------------------------------------------------
		//
		//  Methods
		//
		//---------------------------------------------------------------------------------------------------------------------
		
		override protected function createChildren():void
		{
			super.createChildren();
			
			questionInput = new TextArea();
			questionInput.x = questionInput.y = 10;
			questionInput.height = 100;
			questionInput.backgroundColor = CocoUI.themeBackgroundColor;
			questionInput.editable = questionInput.selectable = false;
			addChild(questionInput);
			
			answerList = new List();
			answerList.x = 10;
			answerList.y = 120;
			answerList.gap = 10;
			answerList.allowMultipleSelection = true;
			answerList.mouseScrollEnabled = false;
			answerList.touchScrollEnabled = false;
			answerList.itemRendererClass = AnswerItemRenderer;
			answerList.itemRendererColumnCount = 2;
			addChild(answerList);
			
			cancelButton = new Button();
			cancelButton.label = "取消";
			cancelButton.height = 30;
			cancelButton.addEventListener(MouseEvent.CLICK, cancelButton_clickHandler);
			addChild(cancelButton);
			
			okButton = new Button();
			okButton.label = "确定";
			okButton.height = 30;
			okButton.addEventListener(MouseEvent.CLICK, okButton_clickHandler);
			addChild(okButton);
		}
		
		override protected function commitProperties():void
		{
			super.commitProperties();
			
			questionInput.text = currentQuestion.name;
			answerList.dataProvider = currentQuestion.answers;
		}
		
		override protected function updateDisplayList():void
		{
			super.updateDisplayList();
			
			questionInput.width = width - 20;
			
			cancelButton.width = width/2;
			cancelButton.y = contentHeight - cancelButton.height;
			
			okButton.width = cancelButton.width;
			okButton.x = cancelButton.width;
			okButton.y = cancelButton.y;
			
			answerList.width = contentWidth - 10 - answerList.x;
			answerList.height = okButton.y - 10 - answerList.y;
		}
		
		protected function okButton_clickHandler(event:MouseEvent):void
		{
			var answer:Answer = new Answer();
			answer.questionID = currentQuestion.id;
			answer.studentID = DataManager.getInstance().student.id;
			for each(var answerObject:Object in answerList.selectedItems)
			{
				answer.answers.push(answerObject);
			}
			
			var message:Message = new Message();
			message.type = "answer";
			message.content = answer;
			ServiceManager.getInstance().sendMessage(message);
			
			// 关闭窗口
			PopUpManager.removePopUp(this);
		}
		
		protected function cancelButton_clickHandler(event:MouseEvent):void
		{
			// 关闭窗口
			PopUpManager.removePopUp(this);
		}
		
		public function open(question:Question = null):void
		{
			currentQuestion = question;
			invalidateProperties();
			PopUpManager.centerPopUp(PopUpManager.addPopUp(this));
		}
		
	}
}